;;; .emacs -- Matthew Doty's emacs configuration   -*- mode: emacs-lisp; lexical-binding: t; -*-

;;; Commentary:
;;;   This .emacs is intended to download all dependencies if
;;;  ~/.emacs.d is ever deleted

;;; Code:

;;; Package Management ;;;

(require 'package)
(add-to-list 'package-archives '("melpa" . "https://melpa.org/packages/") t)
(package-initialize)

;;; General Emacs Settings ;;;
(eval-when-compile
  ;; use-package
  (when (not (package-installed-p 'use-package))
    (package-refresh-contents)
    (package-install 'use-package))
  (require 'use-package)
  (setq use-package-always-ensure t)
  )

(use-package cl-lib
  :commands (smart-buffer-region-format smart-mode-buffer-region-format)
  :config
  (defun smart-buffer-region-format (format-region-fn)
    "Format the current region using the provided FORMAT-REGION-FN.
     If no region is selected then format the current buffer."
    (let ((original-point (point)))
      (if mark-active
        (let ((buffer-size-init (buffer-size))
               (start (region-beginning))
               (end (region-end))
               deactivate-mark)
          (funcall format-region-fn start end)
          (let ((new-end (+ end (- (buffer-size) buffer-size-init))))
            ;; TODO: don't be clever, this screws up the linter
            (destructuring-bind (new-point . new-region-far-end)
              (if (eq end original-point)
                (cons new-end start)
                (cons start new-end))
              (set-mark new-region-far-end)
              (goto-char new-point)))
          (activate-mark))
        (progn
          (funcall format-region-fn (point-min) (point-max))
          (goto-char original-point)
          (message "Formatted file %s" (buffer-file-name))))))

  (defun ensure-list (x) (if (listp x) x (list x)))

  (defun smart-mode-buffer-region-format (modes format-region-fn)
    (when (memq major-mode (ensure-list modes))
      (smart-buffer-region-format format-region-fn))))

(use-package emacs
  :commands (previous-frame)
  :config
  ;; Basic Interface
  (fset 'yes-or-no-p 'y-or-n-p)

  (setq
    inhibit-startup-screen      t       ; No startup screen
    disabled-command-function   nil     ; No prompts
    ring-bell-function          'ignore ; No error bell
    interprogram-cut-function   nil     ; No system clipboard (copy)
    interprogram-paste-function nil     ; No system clipboard (paste)
    vc-follow-symlinks          nil     ; Always follow symlinks
    initial-scratch-message     ""      ; No scratch message
    )

  (tool-bar-mode -1)                    ; No tool-bar
  (menu-bar-mode -1)                    ; No menu-bar
  (blink-cursor-mode -1)                ; No cursor blinking
  ;; No scroll bar
  (when (fboundp 'scroll-bar-mode) (scroll-bar-mode -1))

  ;; Don't deselect after copy
  (defadvice kill-ring-save (after keep-transient-mark-active ())
    "Override the deactivation of the mark."
    (setq deactivate-mark nil))
  (ad-activate 'kill-ring-save)

  ;; Status bar
  (setq display-time-day-and-date t)
  (display-time-mode 1)
  (setq-default display-time-24hr-format t)
  (display-battery-mode 1)
  (setq-default battery-mode-line-format " [%b%t]")

  ;; Commenting
  (defun smart-comment-region-function ()
    "Comment the current region.  If no region is selected then comment
     the line of the pointer is on."
    (interactive)
    (if mark-active
      (comment-or-uncomment-region (region-beginning) (region-end))
      (comment-or-uncomment-region
        (line-beginning-position)
        (line-end-position))))

  ;; Elisp
  (defun define-elisp-key (key action)
    "Define a KEY to perform an ACTION in `emacs-lisp-mode'
     and `lisp-interaction-mode'."
    (dolist (mode (list emacs-lisp-mode-map lisp-interaction-mode-map))
      (define-key mode key action)))

  (setq-default lisp-indent-offset 2)

  (defun elisp-format ()
    "Format a region of elisp code or the entire buffer
     if there is no selection."
    (interactive)
    (smart-mode-buffer-region-format
      '(lisp-interaction-mode emacs-lisp-mode)
      #'indent-region))

  (defun elisp-format-buffer ()
    "Format an elisp buffer."
    (when (equal major-mode 'emacs-lisp-mode)
      (indent-region (point-min) (point-max))))
  (add-hook 'before-save-hook #'elisp-format-buffer)

  (defun elisp-evaluate-region-or-expression ()
    "Evaluate a region or expression in elisp."
    (interactive)
    (cond
      ;; If region selected, evaluate the region
      (mark-active
        (eval-region (region-beginning) (region-end) t))
      ;; If at the start of an s-expression, evaluate that s-expression
      ((string= "(" (string (char-after (point))))
        (save-excursion
          (forward-sexp)
          (eval-last-sexp nil)))
      ;; Otherwise just evaluate the last s-expression
      (t (eval-last-sexp nil))))

  (defun elisp-evaluate-buffer ()
    "Evaluate a buffer of elisp expressions."
    (interactive)
    (eval-buffer)
    (message "Elisp evaluated %s" (buffer-file-name)))

  (define-elisp-key (kbd "C-c C-f") #'elisp-format)
  (define-elisp-key (kbd "M-RET")
    #'elisp-evaluate-region-or-expression)
  (define-elisp-key (kbd "s-<return>")
    #'elisp-evaluate-region-or-expression)
  (define-elisp-key (kbd "C-c C-l") #'elisp-evaluate-buffer)

  ;; Eldoc mode
  (setq eldoc-minor-mode-string nil)
  (add-hook 'emacs-lisp-mode-hook  #'eldoc-mode)

  ;; Find file with line number at point
  ;; https://www.emacswiki.org/emacs/FindFileAtPoint#toc6
  (defvar ffap-file-at-point-line-number nil
    "Variable to hold line number from the last `ffap-file-at-point' call.")

  (defadvice ffap-file-at-point (after ffap-store-line-number activate)
    "Search `ffap-string-at-point' for a line number pattern and save it in the `ffap-file-at-point-line-number' variable."
    (let* ((string (ffap-string-at-point)) ;; string/name definition copied from `ffap-string-at-point'
            (name
              (or (condition-case nil
                    (and (not (string-match "//" string)) ; foo.com://bar
                      (substitute-in-file-name string))
                    (error nil))
                string))
            (line-number-string
              (and (string-match ":[0-9]+" name)
                (substring name (1+ (match-beginning 0)) (match-end 0))))
            (line-number
              (and line-number-string
                (string-to-number line-number-string))))
      (if (and line-number (> line-number 0))
        (setq
          ffap-file-at-point-line-number line-number
          ffap-file-at-point-line-number nil))))

  (defadvice find-file-at-point (after ffap-goto-line-number activate)
    "If `ffap-file-at-point-line-number' is non-nil goto this line."
    (when ffap-file-at-point-line-number
      (unwind-protect
        (goto-line ffap-file-at-point-line-number)
        (setq ffap-file-at-point-line-number nil))))

  ;; Window System Configuration
  (when window-system

    (setq
      ;; Launch Eshell instead of scratch buffer
      initial-buffer-choice 'eshell
      ;; Default directory is home directory
      default-directory     "~/"
      )

    ;; Transparency
    (set-frame-parameter (selected-frame) 'alpha '(98 98)))

  ;; Font Ligatures for OS X
  (when (fboundp 'mac-auto-operator-composition-mode)
    (mac-auto-operator-composition-mode)
    (add-to-list 'default-frame-alist '(font . "Hasklig-14"))
    (set-face-attribute 'default t :font "Hasklig-14"))

  (when (memq window-system '(mac ns))
    ;; Use familiar keys in OS X
    (setq-default
      mac-control-modifier 'control
      mac-option-modifier  'meta
      mac-command-modifier 'super))

  (defun previous-frame ()
    "Cycle to the previous frame."
    (interactive) (other-frame -1))

  ;; Emacs prog-mode
  (add-hook 'prog-mode-hook #'display-line-numbers-mode)

  ;; Put customizations in another file
  (setq custom-file "~/.emacs.d/custom.el")
  (load custom-file 'noerror)

  :bind ( :map global-map
          ;; Select all
          ("s-a" . 'mark-whole-buffer)

          ;; Searching ;;

          ;; Fix C-s to run `isearch-forward-regexp'
          ;; instead of `isearch-forward'

          ;; Also set familiar key bindings on the mac

          ;; Forward
          ("C-s" . 'isearch-forward-regexp)
          ("s-f" . 'isearch-forward-regexp)
          ("s-g" . 'isearch-repeat-forward)

          ;; Backward
          ("C-r" . 'isearch-backward-regexp)
          ("s-F" . 'isearch-backward-regexp)
          ("C-S" . 'isearch-backward-regexp)
          ("s-G" . 'isearch-repeat-backward)

          ;; Non-regex searches
          ("C-M-s" . 'isearch-forward)
          ("C-M-r" . 'isearch-backward)

          ;; Save & Quit
          ("s-s" . 'save-buffer)
          ("s-q" . 'kill-emacs)

          ;; Comment line
          ("C-x C-;" . 'smart-comment-region-function)
          ("M-;"     . 'smart-comment-region-function)
          ("s-/"     . 'smart-comment-region-function)

          ;; Open clicked file
          ("<C-mouse-1>" . 'find-file-at-point)
          ("<s-mouse-1>" . 'find-file-at-point)

          ;; Window Management ;;

          ;; New Frame
          ("s-n"   . 'new-frame)
          ("C-S-n" . 'new-frame)

          ;; Close frame
          ("s-w"   . 'delete-frame)
          ("C-S-w" . 'delete-frame)

          ;; Cycle frames
          ("C-`" . 'other-frame)
          ("C-~" . 'previous-frame)

          :map isearch-mode-map
          ;; Repeat search if already in isearch-mode
          ("s-f" . 'isearch-repeat-forward)
          ("s-F" . 'isearch-repeat-backward)
          ("C-S" . 'isearch-repeat-backward)
          ))

;;; Interface ;;;

;; Powerline
(use-package powerline
  :commands (powerline-center-evil-theme powerline-reset)
  :config
  (powerline-center-evil-theme)
  (powerline-reset)
  (require 'uniquify)
  (setq uniquify-buffer-name-style 'reverse)
  )

(use-package powerline-evil)

(use-package diminish)

;; Theme
(use-package cyberpunk-theme
  :when (eq system-type 'darwin)
  :config (load-theme 'cyberpunk t))

(use-package gruvbox-theme
  :unless (eq system-type 'darwin)
  :config (load-theme 'gruvbox-dark-hard t))

;; Frame resizing
(use-package framesize
  :commands (frame-font-bigger frame-font-smaller)
  :bind ( :map global-map
          ("C-=" . frame-font-bigger)
          ("s-=" . frame-font-bigger)
          ("C-+" . frame-font-bigger)
          ("s-+" . frame-font-bigger)
          ("C--" . frame-font-smaller)
          ("s--" . frame-font-smaller)
          ))

;; Save position
(use-package saveplace :config (save-place-mode))

;; Use shell exec path
(use-package exec-path-from-shell
  :when (memq window-system '(mac ns x))
  :config
  (setq exec-path-from-shell-check-startup-files nil)
  (exec-path-from-shell-initialize))

;; Ido
(use-package ido
  :config
  (ido-mode t)
  (setq-default
    ido-use-filename-at-point 'guess
    ido-use-url-at-point      t
    ido-create-new-buffer     'always))

(use-package flx-ido
  :config
  (flx-ido-mode t)
  (setq-default
    ido-enable-flex-matching t
    ido-use-faces            nil))

;; Copy-Pasta
(use-package simpleclip
  :config
  ;; Settup interprogram clipboard interaction
  ;;; For X-Windows (like terminal)
  (when (eq window-system 'x)
    (global-set-key (kbd "C-S-c") #'simpleclip-copy)
    (global-set-key (kbd "C-S-x") #'simpleclip-cut)
    (global-set-key (kbd "C-S-v") #'simpleclip-paste))
  ;;; For OSX
  (when (memq window-system '(mac ns))
    (simpleclip-mode t)))

;;; Prog Mode ;;;

;; Elisp goto definition
(use-package elisp-slime-nav
  :diminish elisp-slime-nav-mode
  :hook ((emacs-lisp-mode . turn-on-elisp-slime-nav-mode)
          (ielm-mode . turn-on-elisp-slime-nav-mode)))

;; Undo Tree
(use-package undo-tree
  :after evil
  :diminish undo-tree-mode
  :commands (undo-tree-undo undo-tree-redo)
  :config (global-undo-tree-mode)
  :bind ( :map global-map
          ("C-z" . undo-tree-undo)
          ("s-z" . undo-tree-undo)
          ("C-S-z" . undo-tree-redo)
          ("s-S-z" . undo-tree-redo)
          ))

;; Completion Suggestions

(use-package company
  :demand
  :diminish company-mode
  :commands (company-indent-or-complete-common)
  :hook (prog-mode . company-mode)
  :config
  (setq
    ;; Bigger popup window
    company-tooltip-limit             20
    ;; Decrease delay before autocompletion popup shows
    company-idle-delay                .3
    ;; Remove annoying blinking
    company-echo-delay                0
    ;; Start autocompletion only after typing
    company-begin-commands            '(self-insert-command)
    company-tooltip-align-annotations t
    ))

;; Parenthesis

(use-package rainbow-delimiters
  :hook (prog-mode . rainbow-delimiters-mode))

(use-package color
  :commands color-complement-hex)

(use-package paren
  :hook (prog-mode . show-paren-mode)
  :config
  (let ((cursor-color (face-attribute 'cursor :background)))
    (set-face-background 'show-paren-match (color-complement-hex cursor-color))
    (set-face-foreground 'show-paren-match cursor-color))
  )

;; Formatting

(use-package ethan-wspace
  :diminish ethan-wspace-mode
  :init  (setq-default mode-require-final-newline nil)
  :commands (ethan-wspace-clean-all clean-up-tabs set-makefile-whitespace-mode)
  :config
  (setq-default
    tab-width         2
    indent-tabs-mode  nil
    )

  ;; Only use tabs when writing makefiles
  (defun clean-up-tabs ()
    "Cleans up tabs if not in `indent-tabs-mode`."
    (unless indent-tabs-mode
      (untabify (point-min) (point-max))))

  (defun set-makefile-whitespace-mode ()
    "Change whitespace settings to accept tabs, in a manner suitable for a Makefile."
    (setq
      ethan-wspace-errors (remove 'tabs ethan-wspace-errors)
      indent-tabs-mode    t))

  :hook ( (prog-mode . ethan-wspace-mode)
          (before-save . ethan-wspace-clean-all)
          (before-save . clean-up-tabs)
          (makefile-mode . set-makefile-whitespace-mode)
          )
  )

;;; Git ;;;

(use-package magit
  :after evil
  :commands (git-blame)
  :config
  (evil-set-initial-state 'git-commit-mode 'normal)

  ;; The commands below have nothing to do with magit, this is just a convenient place for them

  ;; Run git blame at point and put result in message buffer
  ;; Based off: https://jayconrod.com/posts/67/emacs--run-git-blame-on-the-current-line
  (defun chomp-end (string)
    "Chomp tailing whitespace from STRING."
    (replace-regexp-in-string (rx (* (any " \t\n")) eos) "" string))

  (defun git-blame ()
    "Runs `git blame` on the current line and writes the result to the message buffer."
    (interactive)
    (let* ((line-number (save-excursion
                          (goto-char (point-at-bol))
                          (+ 1 (count-lines 1 (point)))))
            (line-arg (format "%d,%d" line-number line-number))
            (current-file-name (buffer-file-name))
            (commit-buf (generate-new-buffer "*git-blame-line-commit*"))
            (commit-id (unwind-protect
                         (with-current-buffer commit-buf
                           (call-process "git" nil commit-buf nil "blame" "-L" line-arg "--" current-file-name)
                           (buffer-substring 1 9))
                         (kill-buffer commit-buf))))
      (if (string= "00000000" commit-id)
        (message "Not Committed Yet")
        (let* ((log-buf (generate-new-buffer "*git-blame-line-log*")))
          (unwind-protect
            (with-current-buffer log-buf
              (call-process "git" nil log-buf nil "log" "-1" "--pretty=%h   %an   %s" commit-id)
              (message "Line %d: %s" line-number (chomp-end (buffer-string))))
            (kill-buffer log-buf))))))

  :bind ( :map global-map
          ("C-c C-b" . git-blame)
          ("C-c C-g" . magit-status)
          )
  )

(use-package git-link
  :after simple-clip
  :commands (git-link)
  ;; :bind ( :map global-map

  ;;         )
  )

(use-package git-gutter
  :diminish git-gutter-mode
  :hook (prog-mode . git-gutter-mode)
  )

;;; Evil ;;;

(use-package evil
  :commands ( evil-set-initial-state
              evil-ex-define-cmd
              set-evil-initial-state-in-normal-mode)
  :init     (evil-mode)          ; Evil has to be global :-/
  :config
  ;; Ignore evil-mode except for prog-mode
  (setq
    evil-default-state  'emacs   ; Make default state be emacs (for repls and stuff)
    evil-default-cursor t        ; Use default emacs cursor
    evil-shift-width    2        ; Use 2 spaces when shifting with <<>>
    )

  ;; Most of the time we don't want to be in evil-mode, except for prog-mode
  (evil-set-initial-state 'prog-mode 'normal)

  ;; Most of the time we don't want to be in evil-mode, except for prog-mode
  (defun set-evil-initial-state-in-normal-mode ()
    "Puts evil in `normal` mode in its initial state."
    (interactive) (evil-set-initial-state major-mode 'normal))
  (add-hook 'prog-mode-hook #'set-evil-initial-state-in-normal-mode)

  ;; Custom behavior to keep evil from zealously killing emacs when in window-system
  ;; TODO: Incorporate http://zuttobenkyou.wordpress.com/category/emacs/
  (when window-system
    (defun save-and-kill-buffer ()
      (interactive)
      (save-buffer)
      (kill-buffer))

    (define-key evil-normal-state-map "ZZ" 'save-and-kill-buffer)
    (define-key evil-normal-state-map "ZQ" 'evil-delete-buffer)
    (evil-ex-define-cmd "q[uit]" 'evil-delete-buffer)
    (evil-ex-define-cmd "wq" 'save-and-kill-buffer))

  ;; Enable M-. for goto-definition
  ;; https://emacs.stackexchange.com/a/38127
  (define-key evil-normal-state-map (kbd "M-.")
    `(menu-item "" evil-repeat-pop :filter ,(lambda (cmd) (if (eq last-command 'evil-repeat-pop) cmd))))
  (define-key evil-insert-state-map (kbd "M-.")
    `(menu-item "" evil-repeat-pop :filter ,(lambda (cmd) (if (eq last-command 'evil-repeat-pop) cmd))))

  :bind ( :map evil-motion-state-map
          ("M-S-z" . evil-emacs-state)
          ("C-z" . nil)
          :map evil-normal-state-map
          ("C-a" . move-beginning-of-line)
          ("C-e" . move-end-of-line)
          :map evil-visual-state-map
          ("C-a" . move-beginning-of-line)
          ("C-e" . move-end-of-line)
          :map evil-insert-state-map
          ("C-a" . move-beginning-of-line)
          ("C-e" . move-end-of-line)
          ))

;; Eshell
(use-package eshell
  :commands (eshell/open eshell/view)
  :after evil
  :config
  ;; evil is awkward in eshell-mode
  (evil-set-initial-state 'eshell-mode 'emacs)
  (defun eshell/open (file)
    "Eshell command to open a FILE."
    (find-file file))
  (defun eshell/emacs (file)
    "Hack `emacs' to just open a FILE in eshell."
    (eshell/open file))
  (defun eshell/vi (file)
    "Hack `vi' to just open a FILE in eshell."
    (eshell/open file))
  (defun eshell/openo (file)
    "Eshell command to open a FILE in another window."
    (find-file-other-window file))
  (defun eshell/view (file)
    "Hack `view' to open a file in read-only mode."
    (find-file-read-only file))
  (defun eshell/bat (file)
    "Eshell command to open a FILE (read only)."
    (eshell/view file))
  (defun eshell/less (file)
    "Hack less to open a FILE (read only)."
    (eshell/view file))
  (defun eshell/more (file)
    "Hack more to open a FILE (read only)."
    (eshell/view file))
  )

;; Shell scripting
(use-package sh-script
  :commands (sh-format sh-format-buffer)
  :config
  (setq sh-basic-offset 2)

  (defconst sh-format-cmd (format "shfmt -i %d" sh-basic-offset))

  ;; TODO: Preserve window position
  ;; TODO: Check if shfmt exists
  ;; TODO: Check if shfmt exited properly
  ;; TODO: Check if leading whitespace
  ;; TODO: Preserve region trailing whitespace
  (defun sh-format ()
    "Format a region of shell commands or the entire buffer if there is no selection."
    (interactive)
    (smart-mode-buffer-region-format
      'sh-mode
      (lambda (start end)
        (shell-command-on-region start end sh-format-cmd (buffer-name) t))))

  (define-key sh-mode-map (kbd "C-c C-f") #'sh-format)

  (defun sh-format-buffer ()
    "Format a buffer of shell commands."
    (when (equal major-mode 'sh-mode)
      (let ((original-point (point)))
        (shell-command-on-region (point-min) (point-max) sh-format-cmd (buffer-name) t)
        (goto-char original-point))))
  (add-hook 'before-save-hook #'sh-format-buffer)
  )

;;; Nix Mode ;;;
(use-package nix-mode
  :mode "\\.nix\\'")

;;; Flycheck Mode ;;;
(use-package flycheck
  :diminish flycheck-mode
  :commands (global-flycheck-mode flycheck-mode flycheck-add-next-checker)
  :config (global-flycheck-mode)
  :hook ((emacs-lisp-mode . flycheck-mode)
          (sh-mode . flycheck-mode)))

;;; Json Mode ;;;
(use-package json-mode
  :after evil
  :config (evil-set-initial-state 'json-mode 'normal))

;;; YAML ;;;
(use-package yaml-mode
  :after evil
  :config (evil-set-initial-state 'yaml-mode 'normal))

;;; Haskell ;;;
(use-package flycheck-haskell
  :after flycheck
  :commands flycheck-haskell-setup
  :hook (flycheck-mode . flycheck-haskell-setup))

(use-package haskell-mode
  :after evil
  :init (evil-set-initial-state 'haskell-cabal-mode 'normal)
  :config
  (remove-hook 'haskell-mode-hook 'turn-on-haskell-indentation)
  (add-hook 'haskell-mode-hook #'interactive-haskell-mode)
  (setq-default haskell-indent-offset 2)
  (setq haskell-interactive-popup-errors  nil
    haskell-process-type                  'auto
    haskell-tags-on-save                  t
    haskell-process-args-stack-ghci
    '("--ghci-options=-ferror-spans -fshow-loaded-modules"
       "--no-build"
       "--no-load"))
  ;;(add-hook 'before-save-hook #'haskell-mode-stylish-buffer)
  :bind
  (:map haskell-mode-map
    ("C-c C-l" . haskell-process-load-file)
    ("C-c C-g" . projectile-configure-project)
    ("C-c C-c" . projectile-compile-project)
    ("C-`"     . haskell-interactive-bring)
    ("C-c c"   . haskell-process-cabal)
    ("C-c C-t" . haskell-process-do-type)
    ("C-c C-i" . haskell-process-do-info)
    ("C-c C-f" . haskell-cabal-visit-file)
    ;; Use dante-mode or intero-mode to jump to def with "M-."
    ("M-,"     . pop-tag-mark)
    )
  )

(use-package dante
  :after flycheck
  :config
  (flycheck-add-next-checker 'haskell-dante '(warning . haskell-hlint))
  :hook (dante-mode . flycheck-mode)
  )

(use-package intero)

;;; Python ;;;
;; company-jedi wires up jedi to be a backend
;; for company-mode autocompletion.
(use-package company-jedi
  :hook ((python-mode . jedi:setup))
  :commands (enable-company-jedi)
  :config
  (setq-default
    jedi:complete-on-dot t
    jedi:use-shortcuts   t)
  (defun enable-company-jedi ()
    (add-to-list 'company-backends 'company-jedi))
  (add-hook 'python-mode-hook #'enable-company-jedi)
  )

;;; Rust Mode ;;;
(use-package toml-mode
  :after evil
  :config (evil-set-initial-state 'toml-mode 'normal) )

(use-package projectile
  :diminish projectile-mode)

(use-package racer
  :diminish race-mode)

(use-package rust-mode
  :after (racer projectile)
  :hook ((rust-mode . racer-mode)
          (rust-mode . eldoc-mode)
          (rust-mode . projectile-mode))
  :bind (:map rust-mode-map
          ("TAB" . #'company-indent-or-complete-common)
          ("M-." . #'racer-find-definition))
  :config
  (setq
    rust-format-on-save       t
    ido-use-filename-at-point nil)
  )

(provide 'dot-emacs)
;;; dot-emacs ends here
